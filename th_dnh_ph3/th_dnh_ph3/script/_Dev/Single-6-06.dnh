#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Pong"]
#Text["..."] 
#BGM["./bgm/bgm - Bhava-Agra As Seen Through a Child's Mind.mp3"]
#System["./system/Default_System.txt"]
#Background["./system/Background_Sky.txt"]

#include "./Index.txt"

let ball;

let timer = -37; //37

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(2000);
	}
	case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(5000000);
    }
	case(EV_REQUEST_IS_DURABLE_SPELL) {
		SetScriptResult(true);
	}
//	case(EV_REQUEST_IS_LAST_SPELL){
//		SetScriptResult(true);
//	}
	
	case(EV_REQUEST_TIMER) { SetScriptResult(timer*60); } //4500
}
@Initialize
{
	SetCommonData("CurrentSC","\"Pong\"");
	SetCommonData("Difficulty","Extra");
	
	boss = spawnBoss();
	
	ObjEnemy_SetDamageRate(boss,0,0);
	SetShotAutoDeleteClip(64,96,64,64);
	
	MainTask;
	

}
@MainLoop
{

	if(ObjEnemy_GetInfo(boss, INFO_LIFE) <= 0 && !finishing)
	{
		finishing = true;
		TBreakSpell([]);
	}
	else {
		SetBossHitbox(boss,ex,ey);
	}
	frame++;
	yield;
}


task MainTask {
	
	ObjEnemy_SetDamageRate(boss,0,0);
	ObjMove_SetDestAtFrame(boss, cx, cy-120, 60);
	
	SetSpellCard;
//	MCircNextAlpha = 80;
//	Obj_SetValue(objSpellCircle,"MaxAlpha",64);
	
	ObjEnemyBossScene_StartSpell(objScene);
	ObjEnemyBossScene_SetSpellTimer(objScene,timer*60); //5760
	
//	SetCaution;
	SetBossBlur(true);
	AuraRainbow(boss,255,255,255);

	//ConcentrationA2(boss,"HEXAGON",90,3,10,[255,0,255]);
	
	// ball = CreateShotA2(cx,cy,0,0,0,0, FLOWER_STILL_WHITE ,30);
	// ObjShot_SetSpellResist(ball,true);
	// ObjRender_SetColor(ball,255,0,255);
	// ObjRender_SetAngularVelocity(ball,2);
	
	ball = SpawnBall;
	
	wait(90);
	Tmovement(cx-120,cx+120, cy-90,cy-60, 60,240, [MOVE_PLAYERBIAS,2,0,MOVE_MAXSPEED,1.5] );
	ObjEnemy_SetDamageRate(boss,24,8);
	frame = 0;

	TShoot;
	TMakeCircles;
	TMonitor;
	AimAssist;
	FlashText;
}




task TShoot {
	
	let shot;

	let a=45;
	
	
	
	let count = 0;
	
	while(!Obj_IsDeleted(boss)) {
		SE_Play(se_chime,40);
		
		loop(2){

			shot = CreateShotB2(rand(0,sx),0, rand(-1,1),0, 0,1,  1,1.5, DOT_YELLOW+500, 15);
			setval(shot,"NoInfluence",NULL);
			ObjRender_SetAlpha(shot,200);
	
		count++;
		}
		
		wait(5); a+=135;
	}
}

task Trail(shot){
	wait(rand(0,10));
	while(!Obj_IsDeleted(shot)){
		AfterImageShot(shot,GetDelayColor(shot),0,1,40,BLEND_ADD_ARGB,21);
		wait(10);
	}
}


task TMakeCircles{
	let las; let dir=1;
	let circ; let circ2;
	
	SE_Play(se_charge3b,100);
	SetAttackPose(90);
	GlowCircle(ex,ey,[255,0,255],0.1,3,90);
	wait(90);
	
	
	//player's circle
	circ = InfluenceCircleA1(px,py,50,INFL_ADDPATTERN,AIM_OBJ,[1,180,0.5,0,4,LARGE_GRAY,GetPlayerObjectID]);
	//boss's circle
	circ2= InfluenceCircleA1(ex,ey,50,INFL_ADDPATTERN,AIM_OBJ,[1,180,0.5,0,4,LARGE_GRAY,boss]); 

	
	setval(circ,"RandAngle",[-10,10]);setval(circ2,"RandAngle",[-10,10]);
	setval(circ,"CheckRand",[-10,10]);setval(circ2,"CheckRand",[-10,10]);
	setval(circ2,"ConvertColor",[255,0,0]); setval(circ,"ConvertColor",[128,0,255]);
	setval(circ,"CheckExtra",NULL); setval(circ2,"CheckExtra",NULL);
	setcolor(circ2,[255,0,0]); setcolor(circ,[255,0,255]);
	setval(circ2,"RandAngle",[-60,60]);
	
	task tupdatecirc{
		while(!isnull(circ)){
			ObjRender_SetPosition(circ,px,py,0);
			ObjRender_SetPosition(circ2,ex,ey,0);
			yield;
		}
	}
	tupdatecirc;

}



task TMonitor{
	while(!Obj_IsDeleted(boss)){
		if(isOffScreen(ball)){
			killplayer;
			loop(120){
				DeleteShotAll(TYPE_ALL,TYPE_FADE);
				yield;
			}
			SpawnBall;
		}
		if(getvald(ball,"LastInfluenced",-99) == frame){BallIFrames;}
		yield;
	}
}




function SpawnBall{
	ball = CreateShotA2(cx,cy,0,0,0,0, LARGE_GRAY ,30);
	ObjShot_SetSpellResist(ball,true);
//	ObjRender_SetColor(ball,255,0,255);

//	setpos(ball,cx,cy);
//	setrpos(ball,cx,cy,0);
//	ObjMove_SetSpeed(ball,0);
//	ObjMove_SetAcceleration(ball,0);

	
	
	return ball;
}

let ifr = false;
task BallIFrames{
	setval(ball,"NoInfluence",NULL);
	ifr=true;
	
	wait(15);
	remval(ball,"NoInfluence");
	ifr=false;
}

task DamageBoss{
	loop(10){addlife(-rand(1,10));yield;}
}

let lasthit = NULL; let PLAYER=0; let BOSS=1;

task AimAssist{
	while(!Obj_IsDeleted(boss)){
		while(isnull(ball)){yield;}
		if(getcolor(ball)==[255,0,0]){
			TurnToPoint(ball, 1, px, py);
			if(lasthit==PLAYER){ if(GetPlayerState==STATE_NORMAL){DamageBoss;} lasthit=BOSS;}
		}else if(getcolor(ball)==[128,0,255]){
			TurnToPoint(ball, 2, ex, ey);
			if(lasthit==BOSS){addscore(1000); lasthit=PLAYER;}
		}
		yield;
	}
}





function FlashText {
	while(isnull(ball)){yield;}
	let text = makeText(cx,cy,32,[255,255,128],ALIGNMENT_CENTER,255,"Hit the ball into the boss![r]Don't let it hit the edge of the screen!");
	let i=0;
	while(getcolor(ball)==[255,255,255]){
		ObjRender_SetAlpha(text,128+128*abs(sin(15*i)));
		yield; i++;
	}
	Obj_FadeDelete(text);
}