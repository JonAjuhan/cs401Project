#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Ribbon \"Butterfly Dream Box\""]
#Text["..."] 
#BGM["./bgm/bgm - CC-CGR.ogg"]
#System["./system/Default_System.txt"]
#Background["./system/Background_Sky.txt"]

#include "./Index.txt"

let timer = 37; //37

@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(2000);
	}
	case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(5000000);
    }
	case(EV_REQUEST_IS_DURABLE_SPELL) {
		SetScriptResult(true);
	}
//	case(EV_REQUEST_IS_LAST_SPELL){
//		SetScriptResult(true);
//	}
	
	case(EV_REQUEST_TIMER) { SetScriptResult(timer*60); }
}
@Initialize
{
	SetCommonData("CurrentSC","Ribbon \"Butterfly Dream Box\"");
	SetCommonData("Difficulty","Hard");
	
	boss = spawnBoss();
	
	ObjEnemy_SetDamageRate(boss,0,0);
	//SetShotAutoDeleteClip(64,96,64,64);
	SetShotAutoDeleteClip(16,16,16,16);
	//SetShotAutoDeleteClip(32,32,32,32);
	
	MainTask;
	

}
@MainLoop
{

	if(ObjEnemy_GetInfo(boss, INFO_LIFE) <= 0 && !finishing)
	{
		finishing = true;
		TBreakSpell([]);
	}
	else {
		SetBossHitbox(boss,ex,ey);
	}
	frame++;
	yield;
}


task MainTask {
	ObjEnemy_SetDamageRate(boss,0,0);
	ObjMove_SetDestAtFrame(boss, cx, cy-120, 60);
	
	SetSpellCard;
	MCircNextAlpha = 80;
	Obj_SetValue(objSpellCircle,"MaxAlpha",64);
	
	ObjEnemyBossScene_StartSpell(objScene);
	ObjEnemyBossScene_SetSpellTimer(objScene,timer*60); //5760
	
	//SetCaution;
	//SetBossBlur(true);
	AuraRainbow(boss,255,255,255);
	TSpellImmune;

	//ConcentrationA2(boss,"HEXAGON",90,3,10,[255,0,255]);
		
	wait(90);
	Tmovement(cx-120,cx+120, cy-90,cy-60, 60,240, [MOVE_PLAYERBIAS,2,0,MOVE_MAXSPEED,1.5] );
	ObjEnemy_SetDamageRate(boss,24,8);
	frame = 0;
	
	
	TShoot;
	TExplodeShots;
	//TRose;
}






task TShoot{
	let shot; let shots; let pos; let x; let y;
//	wait(90);
	wait(35);
	let a = 0; let t; let r;
	let N=100; let r0 = 10; //50
	let dir = 1;
	while(!Obj_IsDeleted(boss)){	
		SetAttackPose(40);
		wait(20);
		
		shots = [];
		ascent(k in 3..6){
			ascent(i in 0..N){
				t = 360*i/N;
				pos = rotate(r0*cos(t)^3,r0*sin(t)^3,a);
				x = swdx+pos[0]; y = swdy+pos[1];
				r = getdist(swdx,swdy,x,y)/r0;
				
				shot = CreateShotA2(x,y,0,t+a,0.15*r,k*r, BALL_MAGENTA+500-randBin*2,15);
				CustWrap(shot);
				ObjMove_AddPatternA2(shot,15,NO_CHANGE,NO_CHANGE, 0.3*r*(k/5), 2*r*(5/k)*dir,  k*r);
				ObjMove_AddPatternA2(shot,30,NO_CHANGE,NO_CHANGE, 0.05*r*(k/5), 1*r*(k/5)*dir,  k*r);
				setval(shot,"PatternMaxSpeed",k*r/2* (1-0.5*(i%2)) );
				shots=shots~[shot];
			}
		}
		SE_Play(se_chime2,100);
		a += 15; dir*=-1;
		wait(150);
		SE_Play(se_charge2,100);
		ascent(i in 0..length(shots)){
			shot = shots[i];
			if(getvald(shot,"Wrapped",false)){
				ObjMove_Still(shot,60);
				Tremble(shot,1,1,60);
			}else{ObjShot_FadeDelete(shot);}
		}
		wait(60);
		ascent(i in 0..length(shots)){
			shot = shots[i];
			if(getvald(shot,"Wrapped",false)){
				ObjMove_SetAcceleration(shot,0.05);
				ObjMove_SetAngle(shot,GetAngleToPlayer(shot));
				ObjMove_SetMaxSpeed(shot,10);
			}
		}
		Drain(px,py,6,130);
	}
}

task CustWrap(shot){
	ObjRender_SetAlpha(shot,64);
	ObjShot_SetIntersectionEnable(shot,false);
	ObjShot_SetSpellResist(shot,true);
	
	while(!isOffScreen(shot)){yield;}	
	wrap(shot);

	ObjRender_SetAlpha(shot,255);
	ObjShot_SetIntersectionEnable(shot,true);
	ObjShot_SetSpellResist(shot,false);
	ObjMove_SetMaxSpeed(shot,getvald(shot,"PatternMaxSpeed",2.5));
	setval(shot,"Wrapped",true);
}




task TExplodeShots{
	let shot0; let shot;

	let N = 30;
	let a0;
	let dA = 6; //13
	let s0 = 1; let s1 = 3;
	let dS = (s1-s0)/N;

	while(!Obj_IsDeleted(boss)){	
		wait(120);
		
		shot0 = CreateShotA2(swdx,swdy,3,atan2(py-swdy,px-swdx),-10.25,5, LARGE_RED+500,20);
		ObjMove_SetDestAtWeight(shot0,px,py,60,10);
		SE_Play(se_chime2,100);
		a0=atan2(py-swdy,px-swdx) -N/2*dA;
		wait(60);
		let x0 = getx(shot0); let y0 = gety(shot0); 
		
		ascent(i in 0..N){
			//shot = CreateShotA2(x0,y0, 0, a0+dA*i, 0.05, s0+dS*i, BUTT_BLUE+500, 15);
			shot = CreateShotA2(x0,y0, 0, a0+dA*i, 0.05, s0+dS*i, BUTT_BLUE+500, 15);
			ObjShot_SetSpellResist(shot,true);
		}
		SE_Play(se_chime2,100);
		Obj_Delete(shot0);
		dA*=-1;
		
		
	}
}
