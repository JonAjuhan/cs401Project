// #include "./Resources.txt"
#include "./EventList.txt"
#include "./DirPaths.txt"
//others interface with functions in Functions.txt
//with same names/parameters as these
//that notify this script to actually call these ones.


let music;

@Initialize{
	SetScriptID(SCRIPT_MUSIC);
	music = GetCommonData("MusicObject",ObjSound_Create);
	SetCommonData("MusicObject",music);
}

@Event{
	alternative(GetEventType)
	case(EV_SET_SONG){
		let args = GetEventArgument(0);
		let title = args[0];
		let loopstart; let loopend;
		if(length(args)>2){
			loopstart = ator(args[1]);
			loopend = ator(args[2]);
		}else{
			let tmp = GetSongLoop(title);
			loopstart = tmp[0]; loopend = tmp[1];
		}
		SetBGMLoop(title,loopstart,loopend);	
	}
	case(EV_FADE_SONG){
		FadeBGM();
	}
	case(EV_STOP_SONG, 16){ //EV_PAUSE_ENTER
		ObjSound_SetRestartEnable(music,true);
		ObjSound_Stop(music);
	}
	case(EV_START_SONG,17){ //EV_PAUSE_LEAVE
		ObjSound_Play(music);
		ObjSound_SetRestartEnable(music,false);
	}
}

@MainLoop{
	yield;
}










function SetBGM(title){
	let looppos = GetSongLoop(title);
	SetBGMLoop(title,looppos[0],looppos[1]);
}
task SetBGMLoop(title,loopstart,loopend){
	let vol = GetAreaCommonData("CONFIG","BGM Volume",1.00);
	if(ObjSound_IsPlaying(music)){
		descent(i in 0..60){
			ObjSound_SetVolumeRate(music,100*vol*(i/60)^0.5);
			yield;
		}
		ObjSound_Stop(music);
	}
	// if(GetCommonData("CurrentBGM","") != title ){
		// ObjSound_SetRestartEnable(music,false);
		// Obj_Delete(music);
		// music = ObjSound_Create;
		// SetCommonData("MusicObject",music);
	// }
	SetCommonData("CurrentBGM",title); SetCommonData("MusicObject",music);
	// if(!(noStage&&noPlural&&noSingle&&!noPackage)){
	if(GetCommonData("IsStage",false)||GetCommonData("IsPlural",false)||GetCommonData("IsSingle",false)){
		ShowBGMTitle("BGM - "~title);
	}
	ObjSound_Load(music,dirbgm~"bgm - "~title~".ogg");
	ObjSound_SetSoundDivision(music, SOUND_BGM);
	ObjSound_SetLoopEnable(music,true);
	if(loopend<=loopstart){let tmp=GetSongLoop(title);loopstart=tmp[0];loopend=tmp[1];}
	// ObjSound_SetRestartEnable(music,true);	
	ObjSound_SetLoopTime(music,loopstart,loopend);
	ObjSound_SetVolumeRate(music,100*vol);
	ObjSound_Play(music);	
}


task FadeBGM{
	descent(i in 0..60){
		ObjSound_SetVolumeRate(music,100*i/60);
		yield;
	}
	ObjSound_Stop(music);
}





function PauseBGM{
	ObjSound_Stop(music);
}
function ResumeBGM{
	ObjSound_Play(music);
}






task ShowBGMTitle(title){
	let subtitle = "";
	let arr = SplitString(title,"@");
	if(length(arr)>1){
		title = arr[0];
		subtitle = "Arrange: ["~arr[1]~"]";
	}
	let tmp;
	tmp = GetCommonData("BGMTitle","");
	if(tmp!=""){ title = tmp; SetCommonData("BGMTitle",""); };
	tmp = GetCommonData("BGMSubtitle","");
	if(tmp!=""){ subtitle = tmp; SetCommonData("BGMSubtitle",""); };	
	let sx = GetStgFrameWidth; let sy = GetStgFrameHeight;
	//let obj = makeText(sx-15,sy-20,16,[0,0,255],ALIGNMENT_RIGHT, 0, title);
	let obj  = GetCommonData("BGMTitleObject",ObjText_Create);
	SetCommonData("BGMTitleObject",obj);
	Obj_SetVisible(obj,true);
	ObjRender_SetPosition(obj, -15,GetStgFrameHeight-32,0); //-16
	ObjText_SetMaxWidth(obj,sx);
	ObjText_SetHorizontalAlignment(obj,ALIGNMENT_RIGHT);
	ObjText_SetText(obj,title);

	let obj2 = GetCommonData("BGMSubtitleObject",ObjText_Create);
	SetCommonData("BGMSubtitleObject",obj2);
	Obj_SetVisible(obj2,true);
	ObjRender_SetPosition(obj2,-15,GetStgFrameHeight- 16,0); //
	ObjText_SetMaxWidth(obj2,sx);
	ObjText_SetHorizontalAlignment(obj2,ALIGNMENT_RIGHT);
	ObjText_SetText(obj2,subtitle);
	
	//Less obtrusive
	// ObjText_SetFontColorTop(   obj, 180, 180, 255);
	// ObjText_SetFontColorTop(   obj2, 180, 180, 255);
	// ObjText_SetFontColorBottom(obj,  64,  64, 255);
	// ObjText_SetFontColorBottom(obj2,  64,  64, 255);
	//More obtrusive
	ObjText_SetFontColorTop(   obj,  255, 255, 255);
	ObjText_SetFontColorTop(   obj2, 200, 200, 255);
	ObjText_SetFontColorBottom(obj,  180, 180, 255);
	ObjText_SetFontColorBottom(obj2, 180, 180, 255);

	ObjText_SetFontBorderColor(obj,   0,   0, 255);
	ObjText_SetFontBorderColor(obj2,   0,   0, 255);
	ObjText_SetFontBorderType(obj, BORDER_FULL);
	ObjText_SetFontBorderType(obj2, BORDER_FULL);
	ObjText_SetFontBorderWidth(obj, 1);
	ObjText_SetFontBorderWidth(obj2, 1);
	
	
	ObjText_SetFontType(obj, "Cirno");
	ObjText_SetFontType(obj2, "THSpatial");
	
	ObjText_SetFontSize(obj, 20); //16
	ObjText_SetFontSize(obj2,12); //12
	
	Obj_SetRenderPriorityI(obj,79.9999);
	Obj_SetRenderPriorityI(obj2,79.9999);
	
	ObjRender_SetAlpha(obj2,0);
	descent(i in 0..40){ //100
		ObjRender_SetY(obj ,sy-32+i); //sy-20+i
		ObjRender_SetAlpha(obj ,200-2*i);
		yield;
	}
	descent(i in 0..100){ //100
		// ObjRender_SetY(obj2,sy-16+i);
		ObjRender_SetAlpha(obj2,200-2*i);
		yield;
	}
	loop(180){yield;} //120
	function qerp(start,control,end,t){return (1 - t)^2 * start + 2*(1-t)*t * control + t^2 * end;}
	ascent(i in 0..120){ //120
		// ObjRender_SetX(obj,-(15+sx/120*i) );
		ObjRender_SetX(obj ,qerp(-15,-15+(0.15*sx),-15-sx,i/120));
		ObjRender_SetX(obj2,qerp(-15,-15+(0.15*sx),-15-sx,i/120));
		yield;
	}
	Obj_SetVisible(obj ,false);
	Obj_SetVisible(obj2,false);
}






function GetSongLoop(song){
	let musicarr = 
	[
		"Song of the Night Sparrow ~ Night Bird",
		"Deaf to All But the Song",           
		"Last Remote@Magnum Opus",           
		"Hartmann's Youkai Girl@Magnum Opus",    
		"Bhava-Agra as Seen Through a Child's Mind",
		"Kid's Festival ~ Innocent Treasures",
		"Flight of the Bamboo Cutter ~ Lunatic Princess",
		"Flawless Clothing of the Celestials"
	];
	SetCommonData("MusicList",musicarr);
	
	
	alternative(song)
	case("Song of the Night Sparrow ~ Night Bird"){         return [145.8    , 292.4   ]; }
	case("Deaf to All But the Song"){                       return [ 70.7    , 141.38  ]; }
	case("Last Remote@Magnum Opus"){                        return [ 88.29   , 271.67  ]; }
	case("Hartmann's Youkai Girl@Magnum Opus"){             return [ 17.12   , 298.97  ]; }
	case("Bhava-Agra as Seen Through a Child's Mind"){      return [ 66.91   , 115.25  ]; }
	case("Kid's Festival ~ Innocent Treasures"){            return [153.8865 , 297.23  ]; }
	case("Flight of the Bamboo Cutter ~ Lunatic Princess"){ return [ 55.56   , 197.72  ]; }
	case("Flawless Clothing of the Celestials"){            return [ 36.06   , 100.05  ]; }
	
	others{return [0,0];}
}





